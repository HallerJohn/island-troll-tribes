package PetReleasing

// Standard library imports:
import ClosureEvents
import ChannelAbilityPreset
import ClosureTimers
import Lodash

// Local imports:
import ColorUtils
import LocalObjectIDs
import LocalAssets
import Pets
import StringExtensions
import Tribe
import ToolTipsUtils
import PlayerExtensions

class ReleasePetAbility extends ChannelAbilityPreset
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId, 1, true)
        this.setAnimationNames("spell,throw")
        this.setIconNormal(LocalIcons.bTNReleasePet)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setArtCaster("")
        this.setCasterAttachmentPoint("")
        this.setCooldown(1, 5.0)
        this.setFollowThroughTime(1, 1.0)
        this.setName("Release Pet")
        this.setHotkeyNormal(hotkey)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, "Release Your Pet"))
        this.setTooltipNormalExtended(1, "Releases your pet back into the wild allowing you to tame another. The released pet will become hostile and attack you.")
        this.setRequirements(UPGD_PET_TAMED_TRUE.toRawCode())
        this.setEditorSuffix("(Wurst)")

@compiletime function createReleaseSpell()
    new ReleasePetAbility(ABILITY_PET_RELEASE, "W", new Pair(1, 0))
    new ReleasePetAbility(ABILITY_PET_RELEASE_SHAPESHIFTER, "S", new Pair(1, 2))

public function releasePet(unit caster)
    // Look up the pet.
    let originalOwner = caster.getOwner()
    let pet = originalOwner.getPet()

    // Deregister the pet.
    originalOwner.removePet()

    originalOwner.setTechResearched(UPGD_PET_TAMED_TRUE, 0)
    originalOwner.setTechResearched(UPGD_PET_TAMED_FALSE, 1)
    // Revert the available abilities.
    // setPetAbilities(originalOwner, false)

    // Compute the new owner.
    let newOwner = pet.getPetGrowthChain().indexOf(pet.getTypeId()) == 0
        ? PLAYER_NEUTRAL_PASSIVE
        : PLAYER_NEUTRAL_AGGRESSIVE

    // Transfer the ownership of the pet.
    pet.setOwner(Player(newOwner), true)

    // Notify the owning tribe.
    let tribe = Tribe.ofPlayer(originalOwner)
    if tribe != null
        tribe.getMembers().forEach() (player member) ->
            member.print("A pet was released!".color(GENERAL_COLOR))

init
    EventListener.onCast(ABILITY_PET_RELEASE) (unit caster) ->
        // Delay relasing pet so that bonus stats can be removed. See PetGrowth.wurst.
        nullTimer() ->
            releasePet(caster)
    EventListener.onCast(ABILITY_PET_RELEASE_SHAPESHIFTER) (unit caster) ->
        // Delay relasing pet so that bonus stats can be removed. See PetGrowth.wurst.
        nullTimer() ->
            releasePet(caster)
