package PetMovementAbility


// Standard Library Imports:
import ChannelAbilityPreset
import ClosureEvents
import ClosureTimers
import LodashExtensions
import Assets

// Local Imports:
import LocalObjectIDs
import PetMovement
import Tribe
import Game
import ToolTipsUtils

let buttonPosX  = 3
let buttonPosY  = 0
let HOTKEY   = "R"
let NAME     = "Toggle Pet Control"
let TOOLTIP  = "Toggle pet control."
let TOOLTIP_EXT = "Toggle between automatic and manual pet controls."
let ORDER_ID = "barkskinoff"

let AUTO_CONTROL_MESSAGE = "Auto pet control."
let MANUAL_CONTROL_MESSAGE = "Manual pet control."


@compiletime function createTogglePetControl() returns ChannelAbilityPreset
    return new ChannelAbilityPreset(ABILITY_TOGGLE_PET_CONTROL, 1, true)
    ..setDummyAbility()
    ..setAnimationNames("")
    ..presetTargetTypes(Targettype.NONE)
    ..setButtonPositionNormalX(buttonPosX)
    ..setButtonPositionNormalY(buttonPosY)
    ..setIconNormal(Icons.bTNSelectUnit)
    ..setHeroAbility(true)
    ..setHotkeyNormal(HOTKEY)
    ..setName(NAME)
    ..setLevels(1)
    ..setTooltipNormal(1, makeToolTipNorm(HOTKEY, TOOLTIP))
    ..setTooltipNormalExtended(1, TOOLTIP_EXT)
    ..presetBaseOrderID(lvl -> ORDER_ID)
    ..setAnimationNames("")
    ..setCheckDependencies(true)
    ..setRequirements(UPGD_PET_TAMED_TRUE.toRawCode())
    ..setEditorSuffix("(Wurst)")


// Used to toggle pet controls stored in `PetMovement.wurst`.
function togglePetControl()

    // If auto controls are currently active.
    if PET_CONTROLS.getAndRemove(EventData.getTriggerPlayer())

        // Print message alerting the switch to manual controls.
        print(MANUAL_CONTROL_MESSAGE)

        // Reset the player's controls to manual mode.
        PET_CONTROLS.put(EventData.getTriggerPlayer(), false)

    // If manual controls are currently active.
    else

        // Print message alerting the switch to auto controls.
        print(AUTO_CONTROL_MESSAGE)

        // Reset the player's controls to auto mode.
        PET_CONTROLS.put(EventData.getTriggerPlayer(), true)

// Initialize all player's controls to auto at the beginning of the game.
function initializeControls()
    Tribe.getTribes().each(t -> t.getMembers().each(member -> PET_CONTROLS.put(member, true)))

init
    EventListener.onCast(ABILITY_TOGGLE_PET_CONTROL, _ -> togglePetControl())

    registerGameStartEvent() ->
        nullTimer() ->
            initializeControls()
