package MagicResistance


// Standard library imports:
import AbilityObjEditing
import BuffObjEditing
import Lodash
import LinkedList

// Local imports:
import LocalObjectIDs
import LocalAssets
import StringExtensions
import ColorUtils

// Max level of magic resistance is 10 which = 100%.
let MAX_LEVEL = 10
// Minimum level of magic resistance is 1 which = 10%.
let MIN_LEVEL = 1
// Magic resist per lvl.
let RESIST_PER_LVL = .1

// List of the buffs for magic resistance ability.
let MAGIC_RESIST_BUFFS = compiletime(map((lvl, _) -> createDummyBuff(lvl), range(MIN_LEVEL, MAX_LEVEL).toList()))

// Creates the buffs.
function createDummyBuff(int lvl) returns int
    return createDummyBuffObject(lvl.toString().color(MAGIC_RESIST_COLOR)+"0% Magic Resist".color(MAGIC_RESIST_COLOR),
                                 "This unit is resisting "+lvl.toString()+"0% of magic damage.", LocalIcons.bTNResistMagic).abilId

@compiletime function createMagicResistAbility() returns AbilityDefinitionRunedBracers
    return new AbilityDefinitionRunedBracers(ABILITY_MAGIC_RESIST)
        ..setLevels(MAX_LEVEL)
        ..presetDamageReduction(lvl -> RESIST_PER_LVL * lvl)

// Used to get a unit's current level of magic resistance.
public function unit.getMagicResist() returns int
    return this.getAbilityLevel(ABILITY_MAGIC_RESIST)

// Used to give a specified amount of magic resistance to a unit.
// Each lvl is 10% magic resistance ie. 1 = 10%, 6 = 60%.
public function unit.setMagicResist(int lvl)
    // Exit if the level is not in the accepted range.
    if lvl > MAX_LEVEL or lvl < MIN_LEVEL
        return
    // If the unit already has magic resistance, remove it.
    if this.hasAbility(ABILITY_MAGIC_RESIST)
        this.removeMagicResist()

    // Give the unit the base level of magic resistance.
    this.addAbility(ABILITY_MAGIC_RESIST)

    // Increment the level of the ability up to the desired level.
    for _ = 1 to lvl - 1
        this.incAbilityLevel(ABILITY_MAGIC_RESIST)

    // Add the buff corresponding to the level of magic resistance.
    this.addAbility(MAGIC_RESIST_BUFFS.get(lvl - 1))

// Used to remove all magic resistance from a unit.
public function unit.removeMagicResist()
    // Remove the buff first as it depends on the actual ability.
    this.removeAbility(MAGIC_RESIST_BUFFS.get(this.getMagicResist() - 1))
    // Remove the magic resist ability.
    this.removeAbility(ABILITY_MAGIC_RESIST)